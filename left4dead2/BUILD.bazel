load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

#
# Left 4 Dead 2 Layer
#

pkg_tar(
    name = "left4dead2_layer",
    srcs = ["@left4dead2//:files"],
    extension = "tgz",
    package_dir = "/opt/game",
)

#
# MetaMod Layer
#

pkg_tar(
    name = "metamod_layer",
    extension = "tgz",
    package_dir = "/opt/game/left4dead2",
    deps = ["@metamod_linux//file"],
)

#
# SourceMod Layer
#

pkg_tar(
    name = "sourcemod_layer",
    empty_dirs = ["/opt/game/left4dead2/addons/sourcemod/configs"],
    extension = "tgz",
    package_dir = "/opt/game/left4dead2",
    deps = ["@sourcemod_linux//file"],
)

#
# Config Layer
#

pkg_tar(
    name = "config_layer",
    srcs = [":server.cfg"],
    extension = "tgz",
    package_dir = "/opt/game/left4dead2/cfg/templates",
)

#
# Final Image
#

pkg_tar(
    name = "entrypoint_layer",
    srcs = [":entrypoint.sh"],
    extension = "tgz",
    package_dir = "/",
    symlinks = {
        "/root/.steam/sdk32/steamclient.so": "/opt/game/bin/steamclient.so",
    },
)

oci_image(
    name = "image",
    base = "@debian_i386_linux_386",
    entrypoint = ["/entrypoint.sh"],
    env = {
        "L4D2_HOST": "",
        "L4D2_HOSTNAME": "",
        "L4D2_MAP": "c8m1_apartment",
        "L4D2_MOTD": "",
        "L4D2_MOTD_ENABLED": "0",
        "L4D2_PASSWORD": "",
        "L4D2_PORT": "27015",
        "L4D2_STEAMGROUP": "",
        "RCON_PASSWORD": "",
    },
    tars = [
        ":left4dead2_layer",
        ":metamod_layer",
        ":sourcemod_layer",
        ":config_layer",
        ":entrypoint_layer",
    ],
)

oci_load(
    name = "image_tarball",
    image = ":image",
    repo_tags = ["left4dead2-server:bazel"],
)

oci_push(
    name = "image_push",
    image = ":image",
    remote_tags = ["latest"],
    repository = "ghcr.io/lanofdoom/left4dead2-server",
)
