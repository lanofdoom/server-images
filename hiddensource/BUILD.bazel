load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@rules_pkg//:mappings.bzl", "pkg_files", "strip_prefix")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

#
# Source Dedicated Server Layer
#

pkg_tar(
    name = "srcds_layer",
    srcs = ["@srcds_windows//:files"],
    extension = "tgz",
    package_dir = "/opt/game",
)

#
# Source SDK Base 2006 Layer
#

pkg_tar(
    name = "sdk_layer",
    srcs = ["@srcds_base_2006_windows//:files"],
    extension = "tgz",
    package_dir = "/opt/game",
)

#
# Hidden Layer
#

pkg_files(
    name = "hidden_files",
    srcs = ["@hidden//:all"],
    strip_prefix = strip_prefix.from_pkg(),
)

pkg_tar(
    name = "hidden_layer",
    srcs = [":hidden_files"],
    extension = "tgz",
    package_dir = "/opt/game",
)

#
# MetaMod Layer
#

pkg_files(
    name = "metamod_files",
    srcs = ["@metamod_windows//:all"],
    strip_prefix = strip_prefix.from_pkg(),
)

pkg_tar(
    name = "metamod_layer",
    srcs = [":metamod_files"],
    extension = "tgz",
    package_dir = "/opt/game/hidden",
)

pkg_tar(
    name = "metamod_vdf_layer",
    srcs = [":metamod.vdf"],
    extension = "tgz",
    package_dir = "/opt/game/hidden/addons",
)

#
# SourceMod Layer
#

pkg_files(
    name = "sourcemod_files",
    srcs = ["@sourcemod_windows//:all"],
    strip_prefix = strip_prefix.from_pkg(),
)

pkg_tar(
    name = "sourcemod_layer",
    srcs = [":sourcemod_files"],
    empty_dirs = ["/opt/game/hidden/addons/sourcemod/configs"],
    extension = "tgz",
    package_dir = "/opt/game/hidden",
)

#
# Authorization Layer
#

pkg_files(
    name = "authorization_files",
    srcs = ["@auth_by_steam_group_windows//:all"],
    strip_prefix = strip_prefix.from_pkg(),
)

pkg_tar(
    name = "authorization_layer",
    srcs = [":authorization_files"],
    extension = "tgz",
    package_dir = "/opt/game/hidden",
)

#
# Config Layer
#

pkg_tar(
    name = "config_layer",
    srcs = [":server.cfg"],
    extension = "tgz",
    package_dir = "/opt/game/hidden/cfg/templates",
)

#
# Final Image
#

pkg_tar(
    name = "entrypoint_layer",
    srcs = [":entrypoint.sh"],
    extension = "tgz",
    package_dir = "/",
)

oci_image(
    name = "image",
    base = "@debian",
    entrypoint = ["/entrypoint.sh"],
    env = {
        "HIDDEN_HOSTNAME": "",
        "HIDDEN_MAP": "hdn_traindepot",
        "HIDDEN_MOTD": "",
        "HIDDEN_PASSWORD": "",
        "HIDDEN_PORT": "27015",
        "RCON_PASSWORD": "",
        "STEAM_GROUP_ID": "",
        "STEAM_API_KEY": "",
    },
    tars = [
        ":srcds_layer",
        ":sdk_layer",
        ":hidden_layer",
        ":metamod_layer",
        ":metamod_vdf_layer",
        ":sourcemod_layer",
        ":authorization_layer",
        ":config_layer",
        ":entrypoint_layer",
    ],
)

oci_load(
    name = "image_tarball",
    image = ":image",
    repo_tags = ["hiddensource-server:bazel"],
)

oci_push(
    name = "image_push",
    image = ":image",
    remote_tags = ["latest"],
    repository = "ghcr.io/lanofdoom/hiddensource-server",
)
