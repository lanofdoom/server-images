load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@rules_pkg//:mappings.bzl", "pkg_files", "strip_prefix")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

#
# Pak0 Layer
#

pkg_files(
    name = "quake3_pak0_files",
    srcs = [
        "@quake3_pak0//file",
    ],
)

pkg_tar(
    name = "quake3_pak0_layer",
    srcs = [
        ":quake3_pak0_files",
    ],
    extension = "tgz",
    package_dir = "/opt/game/baseq3",
)

#
# PK3s Layer
#

pkg_files(
    name = "quake3_pk3s_files",
    srcs = [
        "@quake3_pk3s//:all",
    ],
    strip_prefix = strip_prefix.from_pkg(),
)

pkg_tar(
    name = "quake3_pk3s_layer",
    srcs = [
        ":quake3_pk3s_files",
    ],
    extension = "tgz",
    package_dir = "/opt/game",
)

#
# Binary Layer
#

pkg_files(
    name = "quake3_files",
    srcs = ["@quake3//:all"],
    strip_prefix = strip_prefix.from_pkg(),
)

pkg_tar(
    name = "quake3_layer",
    srcs = [
        ":quake3_files",
    ],
    extension = "tgz",
    package_dir = "/opt/game",
)

#
# Map Cycle
#

pkg_tar(
    name = "mapcycle_layer",
    srcs = [
        ":mapcycle.cfg",
    ],
    extension = "tgz",
    package_dir = "/opt/game/baseq3",
)

#
# Final Image
#

pkg_tar(
    name = "entrypoint_layer",
    srcs = [":entrypoint.sh"],
    extension = "tgz",
    package_dir = "/",
)

oci_image(
    name = "image",
    base = "@debian_linux_amd64",
    entrypoint = ["/entrypoint.sh"],
    env = {
        "Q3_FRAGLIMIT": "20",
        "Q3_HOSTNAME": "",
        "Q3_PASSWORD": "",
        "Q3_SPEED": "320",
        "RCON_PASSWORD": "",
    },
    exposed_ports = [
        "27960/udp",
    ],
    tars = [
        ":quake3_pk3s_layer",
        ":quake3_pak0_layer",
        ":quake3_layer",
        ":mapcycle_layer",
        ":entrypoint_layer",
    ],
)

oci_load(
    name = "image_tarball",
    image = ":image",
    repo_tags = ["quake3-server:bazel"],
)

oci_push(
    name = "image_push",
    image = ":image",
    remote_tags = ["latest"],
    repository = "ghcr.io/lanofdoom/quake3-server",
)
